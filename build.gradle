buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'me.tatarka:gradle-retrolambda:3.3.0'
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'de.felixschulze.gradle:gradle-hockeyapp-plugin:3.5'
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    projectName = 'Template'
    hockeyAppId = '"00000000000000000000000000000000"'
    versionName = gitVersionName
    versionCode = gitVersionCode
    versionHash = gitRefHash
}

def getGitVersionName() {
    def tag = 'git describe --tags'.execute().text.split('-').first()?.trim()
    if (tag =~ /^v.*$/) {
        return tag.substring(1)
    }
    return '1.0.0'
}

def getGitRefHash() {
    return 'git rev-parse HEAD'.execute().text.substring(0, 8)
}

int getGitVersionCode() {
    def versionCode = 1
    (gitVersionName =~ /(\d+)/).collect {
        Integer.parseInt(it[0] as String)
    }.eachWithIndex { num, index ->
        versionCode += (num * (100000 / Math.pow(100, index)))
    }
    def buildId = System.getenv('CI_BUILD_ID')
    if (buildId) {
        versionCode += Integer.parseInt(buildId)
    }
    return versionCode
}