buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'de.felixschulze.gradle:gradle-hockeyapp-plugin:3.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

    // Exclude the version that the android plugin depends on.
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

ext {
    projectName = 'Template'
    hockeyAppId = '"00000000000000000000000000000000"'
    versionName = versionName
    qaVersionSuffix = qaVersionSuffix
    versionCode = versionCode
}

def getQaVersionSuffix() {
    def tag = 'git describe --tags'.execute().text.split('-').first()?.trim()
    if (tag =~ /^v.*$/) {
        return tag.substring(tag.lastIndexOf("."))
    }
    return '1.0.0'
}

def getVersionName() {
    def tag = 'git describe --tags'.execute().text.split('-').first()?.trim()
    if (tag =~ /^v.*$/) {
        return tag.substring(1, tag.lastIndexOf(".")) // exclude build number
    }
    return '1.0.0'
}

int getVersionCode() {
    /**
     * Use the number of seconds / 20 since Jan 1 1970 as the versionCode.
     * This lets us upload a new build at most every 20 seconds until 3331 year.
     */
    return (int)(new Date().getTime() / 20000)
}