buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    projectName = 'Template'
    versionName = versionName
    qaVersionSuffix = qaVersionSuffix
    versionCode = versionCode
}

def getQaVersionSuffix() {
    def tag = 'git describe --tags'.execute().text.split('-').first()?.trim()
    if (tag =~ /^v.*$/) {
        return tag.substring(tag.lastIndexOf("."))
    }
    return '1.0.0'
}

def getVersionName() {
    def tag = 'git describe --tags'.execute().text.split('-').first()?.trim()
    if (tag =~ /^v.*$/) {
        return tag.substring(1, tag.lastIndexOf(".")) // exclude build number
    }
    return '1.0.0'
}

int getVersionCode() {
    /**
     * Use the number of seconds / 20 since Jan 1 1970 as the versionCode.
     * This lets us upload a new build at most every 20 seconds until 3331 year.
     */
    return (int)(new Date().getTime() / 20000)
}