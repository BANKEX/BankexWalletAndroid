apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: '../config/quality/quality.gradle'
apply plugin: 'de.felixschulze.gradle.hockeyapp'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.elegion.android.template"
        minSdkVersion 16
        targetSdkVersion 27

        versionCode rootProject.versionCode
        versionName rootProject.versionName

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        manifestPlaceholders = [HOCKEY_APP_ID: rootProject.hockeyAppId]

        buildConfigField 'String', 'API_BASE_URL', '"https://api.github.com/"'
        buildConfigField "String", "HOCKEY_APP_ID", rootProject.ext.hockeyAppId
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "2048M"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("STOREPASS")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPASS")
        }
        playstore {
            storeFile file(String.valueOf(System.getenv("RELEASE_KEYSTORE")))
            storePassword System.getenv("RELEASE_STOREPASS")
            keyAlias System.getenv("RELEASE_KEYALIAS")
            keyPassword System.getenv("RELEASE_KEYPASS")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }

        releaseQa {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            versionNameSuffix rootProject.qaVersionSuffix
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
}

apply from: '../versions.gradle'

dependencies {
    implementation 'com.android.support:multidex:1.0.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpInterceptorVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation 'com.arello-mobile:moxy:1.5.3'
    kapt 'com.arello-mobile:moxy-compiler:1.5.3'
    implementation 'com.arello-mobile:moxy-app-compat:1.5.3'

    implementation "net.hockeyapp.android:HockeySDK:$hockeyAppVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoViersion"
}

detekt {
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$projectDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

hockeyapp {
    apiToken = System.getenv("HOCKEY_APP_TOKEN")
    notesType = 1
    notify = 1
    notes = System.getenv("CHANGELOG")
    tags = "test"
}