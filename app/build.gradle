apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'de.felixschulze.gradle.hockeyapp'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        applicationId "com.elegion.android.template"
        minSdkVersion 16
        targetSdkVersion 27

        versionCode rootProject.versionCode
        versionName rootProject.versionName

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        manifestPlaceholders = [HOCKEY_APP_ID: rootProject.hockeyAppId]

        buildConfigField 'String', 'API_BASE_URL', '"https://api.github.com/"'
        buildConfigField "String", "HOCKEY_APP_ID", rootProject.ext.hockeyAppId
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries true
        javaMaxHeapSize "2048M"
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("STOREPASS")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPASS")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField 'int', 'TEST_SDK_INT', '21'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField 'int', 'TEST_SDK_INT', '16'
            versionNameSuffix '-test'
        }

        live {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'int', 'TEST_SDK_INT', '16'
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    flavorDimensions 'buildType'

    productFlavors {
        fastDex {
            dimension 'buildType'
            minSdkVersion 21
        }

        prod {
            dimension 'buildType'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
}

apply from: '../versions.gradle'

dependencies {
    implementation 'com.android.support:multidex:1.0.2'

    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"

    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpInterceptorVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"

    implementation 'com.arello-mobile:moxy:1.5.3'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.3'
    implementation 'com.arello-mobile:moxy-app-compat:1.5.3'

    implementation "net.hockeyapp.android:HockeySDK:$hockeyAppVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoViersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}

hockeyapp {
    apiToken = System.getenv("HOCKEY_APP_TOKEN")
    notesType = 1
    notify = 1
    notes = System.getenv("CHANGELOG")
    tags = "test"
}