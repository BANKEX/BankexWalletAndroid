plugins {
    id "org.sonarqube" version "1.2"
}
apply plugin: 'com.android.application'
android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    defaultConfig {
        applicationId "com.elegion.android"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        buildConfigField "String", "HOCKEY_APP_ID", rootProject.ext.hockeyAppId
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("STOREPASS")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPASS")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField 'int', 'TEST_SDK_INT', '21'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'buildType'

    productFlavors {
        dev {
            dimension 'buildType'
            signingConfig signingConfigs.release
        }

        mock {
            dimension 'buildType'
            versionName rootProject.ext.versionName + '-mock'
            signingConfig signingConfigs.debug
        }
        prod {
            signingConfig signingConfigs.release
            buildConfigField "String", "HOCKEY_APP_ID", '""'
            dimension 'buildType'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
}

apply from: '../versions.gradle'

dependencies {
    compile "com.android.support:support-v4:${supportVersion}"
    compile "com.android.support:support-v13:${supportVersion}"
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"

    compile "com.jakewharton:butterknife:$butterVersion"
    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "com.github.bumptech.glide:glide:${glideVersion}"

    compile "io.reactivex:rxandroid:${rxandroidVersion}"
    compile "io.reactivex:rxjava:$rxjavaVersion"
    compile "rxsqlite:library:$rxSqliteVersion"
    provided "rxsqlite:compiler:$rxSqliteVersion"

    devCompile "net.hockeyapp.android:HockeySDK:${hockeyAppVersion}"
    mockCompile "net.hockeyapp.android:HockeySDK:${hockeyAppVersion}"

    testCompile('junit:junit:4.12')
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
}


apply plugin: 'de.felixschulze.gradle.hockeyapp'

hockeyapp {
    apiToken = System.getenv("HOCKEY_APP_TOKEN")
    notesType = 1
    notify = 1
    notes = System.getenv("CHANGELOG")
    tags = "test"
}

apply from: 'jacoco.gradle'
apply from: 'sonar.gradle'
