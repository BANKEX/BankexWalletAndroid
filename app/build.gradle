apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.elegion.android"
        minSdkVersion 15
        targetSdkVersion 20
        versionCode project.buildNumber as int
        versionName "1.0"

        buildConfigField "String", "HOCKEY_APP_ID", "\"${project.hockeyAppId}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("debug.jks")
        }
        release {
            storeFile file(project.keystore)
            storePassword project.storepass
            keyAlias project.keyalias
            keyPassword project.keypass
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        dev
        hockey
        live
    }
}

apply plugin: 'android-apt'

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.6.+'
    compile 'com.squareup.okhttp:okhttp:2.0.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.+'
    compile 'com.squareup:otto:1.3.+'
    compile 'com.squareup.picasso:picasso:2.3.+'
    compile "org.androidannotations:androidannotations-api:3.+"
    compile 'droidkit:droidkit:3.1.+'
    apt "org.androidannotations:androidannotations:3.+"
    hockeyCompile fileTree(dir: 'libs/hockey', include: ['*.jar'])
}

apt {
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName variant.mergedFlavor.applicationId
    }
}

apply plugin: 'droidkit.plugins.redmine'
apply plugin: 'droidkit.plugins.imagemagic'
apply plugin: 'droidkit.plugins.hockeyapp'

redmine {
    trackers '1', '5'
    statuses '3'
    ignoreSslErrors true
    sinceTag 'hockey-'
}

imagemagic {
    flavors 'hockey'
}

hockeyapp {
    flavors 'hockey'
}