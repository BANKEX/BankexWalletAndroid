plugins {
    id "org.sonarqube" version "1.2"
}

apply plugin: 'com.android.application'
apply plugin: "me.tatarka.retrolambda"
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
    }

    defaultConfig {
        applicationId "com.elegion.android"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode rootProject.versionCode
        versionName rootProject.versionName

        buildConfigField 'String', 'API_BASE_URL', '"https://api.vk.com/method"'
        buildConfigField "String", "HOCKEY_APP_ID", rootProject.ext.hockeyAppId
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
        }
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("STOREPASS")
            keyAlias System.getenv("KEYALIAS")
            keyPassword System.getenv("KEYPASS")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            buildConfigField 'int', 'TEST_SDK_INT', '21'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'buildType'

    productFlavors {
        dev {
            dimension 'buildType'
            signingConfig signingConfigs.release
        }

        mock {
            dimension 'buildType'
            versionName rootProject.ext.versionName + '-mock'
            signingConfig signingConfigs.debug
        }

        prod {
            signingConfig signingConfigs.release
            buildConfigField "String", "HOCKEY_APP_ID", '""'
            dimension 'buildType'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.all { jvmArgs '-noverify' }
    }
}

apply from: '../versions.gradle'

dependencies {
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "io.reactivex:rxandroid:$rxandroidVersion"
    compile "io.reactivex:rxjava:$rxjavaVersion"
    compile 'io.reactivex:rxjava-computation-expressions:0.21.0'
    compile "rxsqlite:library:$rxSqliteVersion"

    compile "com.squareup.okhttp3:okhttp:$okhttpVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.1'

    provided "rxsqlite:compiler:$rxSqliteVersion"
    devCompile "net.hockeyapp.android:HockeySDK:$hockeyAppVersion"
    mockCompile "net.hockeyapp.android:HockeySDK:$hockeyAppVersion"
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    compile project(':rxloadermanager')
}


apply plugin: 'de.felixschulze.gradle.hockeyapp'

hockeyapp {
    apiToken = System.getenv("HOCKEY_APP_TOKEN")
    notesType = 1
    notify = 1
    notes = System.getenv("CHANGELOG")
    tags = "test"
}

apply from: 'sonar.gradle'
